plugins {
    alias(libs.plugins.version)
    alias(libs.plugins.spring)
    alias(libs.plugins.vaadin)
    id 'groovy'
    id 'io.freefair.lombok' version '8.12'

}

group = 'net.ofnir'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenLocal()
    mavenCentral()
    // maven {
    //     url = "https://maven.vaadin.com/vaadin-prereleases"
    // }
}

bootRun {
    environment("SPRING_PROFILES_ACTIVE", "dev")
    systemProperties = System.properties
}

bootJar {
    dependsOn vaadinBuildFrontend
    archiveClassifier = "fat"
}

vaadin {
    reactEnable = false // remove for react router and components in the client
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

tasks.named('dependencyUpdates').configure {
    gradleReleaseChannel = 'current'
    revision = 'release'
    rejectVersionIf {
        it.candidate.group=="org.springframework.boot" && !it.candidate.version.startsWith("3.4") ||
        it.candidate.group=="org.apache.groovy" && !it.candidate.version.startsWith("4.")
    }
}

dependencies {
    implementation platform(libs.springboot.bom)
    implementation platform(libs.vaadin.bom)
    implementation platform(libs.groovy.bom)

    implementation "org.apache.groovy:groovy"
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "com.vaadin:vaadin-spring-boot-starter", {
        exclude group: 'com.vaadin', module: 'hilla'
        exclude group: 'com.vaadin', module: 'flow-react'
        exclude group: 'com.vaadin', module: 'vaadin-dev'
    }

    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    runtimeOnly "com.h2database:h2"
    implementation "org.springframework.security:spring-security-crypto"

    developmentOnly platform(libs.springboot.bom)
    developmentOnly platform(libs.vaadin.bom)

    developmentOnly "org.springframework.boot:spring-boot-devtools"
    developmentOnly "com.vaadin:vaadin-dev", {
        exclude group: 'com.vaadin', module: 'hilla-dev'
        exclude group: 'com.vaadin', module: 'copilot'
    }

    // --- TESTS ---

    // Spring Boot test starter (gives you @SpringBootTest, @ActiveProfiles, AssertJ, etc.)
    testImplementation "org.springframework.boot:spring-boot-starter-test"

    // Spock for Groovy 4
    testImplementation "org.spockframework:spock-core:2.4-M1-groovy-4.0"
    testImplementation "org.spockframework:spock-spring:2.4-M1-groovy-4.0"

    // (optional; starter-test already pulls the right JUnit bits)
    // testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
    useJUnitPlatform()
    testLogging {
        events "started", "passed", "skipped", "failed"
    }

}
